# 第二章 函数

函数英文为 Function ，意为功能和用途。在程序中，我们可以把函数理解成为了做某事的一个名称及其参数的组合。

![function](https://zh.wikipedia.org/wiki/File:Function_machine2.svg)

这里输入的就是参数，输出的就是这个函数运算结果。 在 Processing 的 reference 里大多数都是对于函数的解释。

由函数加上分号，构成 Processing 里的程序指令(statement)。

同时我们也可以把函数视为一种**抽象**(abstraction)的方法, 它指**隐藏细节并专注于结果**，这是有别于艺术语境下抽象的另一层意思。

为此我们也可以通过抽象这种方法，自定义一个函数。目的是为了**代码整洁**和**重复调用**，

## 封装

通过自定义一个函数，将不同目的的指令放进去，我们称之为“封装”。似乎听起来有点像“收纳”，我们也可以理解为模块化。当程序运行指令时会先跳到函数定义的地方，然后再跳回来。

如下代码，我们自定了一个名为 bouncingBall() 的自定义函数。

```java
float x = 100;
float y = 100;
float xspeed = 2.5;
float yspeed = 2;

void setup() {
  size(800, 200);
  smooth();
}

void draw() {
  background(255);
  bouncingBall();
}

void bouncingBall() {
  // Add the current speed to the position.
  x = x + xspeed;
  y = y + yspeed;

  if ((x > width) || (x < 0)) {
    xspeed = xspeed * -1;
  }
  if ((y > height) || (y < 0)) {
    yspeed = yspeed * -1;
  }


  // Display circle at x position
  stroke(0);
  strokeWeight(2);
  fill(127);
  ellipse(x, y, 48, 48);
}

```
这样你在看源代码里的时候，就非常清楚了，在 draw() 里只有两行指令。

```java
void draw() {
  background(255);
  bouncingBall();
}
```

我们还可以通过函数这种抽象分装的方法将其拆解成三个自定义函数。

```java
// Modify by akb4q
// Learning Processing
// Daniel Shiffman
// http://www.learningprocessing.com

// Example 7-3: Bouncing ball with functions

// Declare all global variables (stays the same)
float x = 0;
float  y = 0;


float xspeed =2.5;
float  yspeed = 1;

// Setup does not change
void setup() {
  size(640, 360);
}

void draw() {
  background(255);
  move(); // Instead of writing out all the code about the ball is draw(), we simply call three functions. How do we know the names of these functions? We made them up!
  bounce();
  display();
}

// Where should functions be placed?
// You can define your functions anywhere in the code outside of setup() and draw().
// However, the convention is to place your function definitions below draw().

// A function to move the ball
void move() {
  // Change the x location by speed
  x = x + xspeed;
  y = y + yspeed;
}

// A function to bounce the ball
void bounce() {
  // If we’ve reached an edge, reverse speed
  if ((x > width) || (x < 0)) {
    xspeed = xspeed * - 1;
  }
  if ((y > height) || (y < 0)) {
    yspeed = yspeed * -1;
  }
}

// A function to display the ball
void display() {
  stroke(0);
  fill(175);
  ellipse(x, y, 32, 32);
}
```

至于如何什么时候封装，为什么要封装，也是因人而异的，目的为的是你在写代码的时候能有一个清晰的思路，之后回看代码的时候也一目了然。

## 回报/返回值

凡是看到 Void 打头的函数定义，只执行内部指令，它的回报为“空”。 如果是根据输入，获得某个数据类型值的时候，我们一定要指定其回报的数据类型(return DataType)。

```java

// Learning Processing
// Daniel Shiffman
// http://www.learningprocessing.com

// Example 7-4: Using a function that returns a value, distance

void setup() {
  size(200, 200);
}

void draw() {
  background(255);
  stroke(0);

  float d = distance(width/2, height/2, mouseX, mouseY);
  fill(d*3, d*2, d);
  ellipseMode(CENTER);
  ellipse(width/2, height/2, 100, 100);
}

float distance(float x1, float y1, float x2, float y2) {
  float dx = x1 - x2;
  float dy = y1 - y2;
  float d = sqrt(dx*dx + dy*dy);
  return d;
}
```

当鼠标与球体中心点的位置之间的距离发生变化，球的颜色也随之变化。这里自定义了一个函数 ``` distance()``` 。 它的回报类型是浮点数即小数。 其内部就是我们小学的数学知识**勾股定理**。

```java
float distance(float x1, float y1, float x2, float y2) {
  float dx = x1 - x2;
  float dy = y1 - y2;
  float d = sqrt(dx*dx + dy*dy);
  return d;
}
```

我们把函数括号里的值称之为**参数** parameter, 有时你会在其他一些地方看到叫作 argument 。 他们的区别是：当在定义函数的时候，我们叫**形参** parameter ,当调用函数时输入的值叫作**实参** argument 。

```java
float distance(float x1, float y1, float x2, float y2)
 ```  

这里定义的x1, y1, x2, y2 我们叫 “parameter”。

```java
distance(width/2, height/2, mouseX, mouseY);
```

实际调用的时候，把 width/2, height/2, mouseX, mouseY 叫作 “argument” 。

我们需要回以下勾股定理，严格的应称之为“毕达哥拉斯定理（（Pythagoras theorem）”即

![](assets/ 第二章 函数-16f8946b.png)

在 Processing 的坐标系里，将两点分别在横轴和纵轴上的位置相减，所获得的差，即两条直角边的长度，然后通过```sqrt()```求其平方根（square root)。

```
float dx = x1 - x2;
float dy = y1 - y2;
float d = sqrt(dx*dx + dy*dy);
```

最后赋值给变量 d , 通过
``` return ``` 回报该变量给外部。

这里请注意！你会在程序里发现两个叫作 d 的变量。 他们同为数据类型 float。 对于程序来说，变量分为**全局变量**(global variable)和**局部变量**(local variable)，如果是在函数外的，比如写在程序最投上的，是全局变量； 写在函数内的一定是局部变量。他们的作用域是不同的，作为全局变量，你可以在代码的任何地方获取调用，但局部变量只能是在其函数内部。 所以两个变量d 作为局部变量并不冲突。

其实 Processing 有一个自带函数 ``` dist()```, 透过以上代码窥看其“黑箱”的内部。 这是一个非常有用的函数。 同时要感叹一下古人的智慧，据说在毕达哥拉斯的手稿内尽然发现了微积分！
