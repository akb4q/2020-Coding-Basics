# 第一章 逆向工程之 Bouncing Ball



```java
     // The Nature of Code
     // Daniel Shiffman
     // http://natureofcode.com
     // Example 1-1: Bouncing Ball, no vectors
     float x = 100;
     float y = 100;
     float xspeed = 2.5;
     float yspeed = 2;

     void setup() {
       size(800, 200);
       smooth();
     }


     void draw() {
         background(255);
     // Add the current speed to the location.
         x = x + xspeed;
         y = y + yspeed;
         if ((x > width) || (x < 0)) {
             xspeed = xspeed * -1;
         }
         if ((y > height) || (y < 0)) {
             yspeed = yspeed * -1;
         }
     // Display circle at x location
         stroke(0);
         strokeWeight(2);
         fill(127);
         ellipse(x, y, 48, 48);
     }

```
让我们从一个直观的程序所生成的动画入手。请将以上代码在 PDE 里运行。敢这么说，如果看懂以上代码，算 Processing 入门一半。

下面我们一一解构它。


## 结构
这里可以看到两个主要的由两个大括号括起来的程序块。也是 Processing 最主要的两个函数。

```java  
void setup(){

}
```

```java
void draw(){

}
```

注意他们分别以 **void** 的开头。 void 是什么意思？直译为“空”。除了运行，作为函数它不回报出任何值，在之后函数单元会细讲。

对于 Processing 来说 setup()和 draw() 是必要函数，是结构性的，不能没有。当然也有例外，你可以写尝试键入只有一行指令的代码

```
print(“hello world!”);
```
又或者直接运行空白的代码，看看会发生什么。




## 初始
在 setup(){} 中,我们可以看到以**分号**为结尾的两行**指令**，英文叫 Statement。 通常初学者在写指令的时候，很容易遗漏。

```java
size();
smooth();
```


我们把第一行指令理解成：我们要打一个画框，多大呢？800*200，单位是[像素 Pixel](https://zh.wikipedia.org/wiki/%E5%83%8F%E7%B4%A0)。

接下来是画布，要糙一点还是平滑一点。smooth 表示光滑的意思, smooth() 表示图形的质地渲染是光滑的,与其对应的 noSmooth(), 不要光滑。详见[反锯齿 anti-alias](https://zh.wikipedia.org/wiki/%E5%8F%8D%E9%8B%B8%E9%BD%92)。

这就是整个程序的初始，在所有其余代码前运行，且只运行一次。


## 变量
初始化的同时也包括 setup() 之上的那几行代码,

```java
float x = 100;
float y = 100;
float xspeed = 2.5;
float yspeed = 2;
```

他们就是变量的声明。（且是全局变量，后面我们还会接触到局部变量。）

``` java
float x = 100;
float y = 100;
```
把数值 100 分别赋值给 x和 y ，也就是球在程序开始运行的初始位置。

float 意思浮点数的意思,通常意义上就小数,与其对应的是 int 整数。

为什么程序里的小数叫做浮点数,这里涉及到计算机科学内容,关于内存的算法,电脑是如何记忆数值的,至于原理初学同学可以暂时忽略。

只要记住以下这个语法，让电脑记住我们赋予变量的值是什么。

```
    DataType variable = value;

    数据类型 变量名称 = 值;
```


三个项组成的固定语法构成一句变量声明的指令,以分号结尾。

这里再重复一遍，凡是程序指令 statement 都有 ```; ```结尾,告诉电脑,我要执行他们。

如果没有,可以试一试!程序在 PDE 最下方, Console 里会报错:

> syntax error, maybe a missing semicolon?

电脑就是这样的强势,不通融或者称之为傻;这也就是程序语言和自然语言,最大区别,它要求严格的语法！

作为初学者,我们可以不断试错,修改一些地方,看看电脑会报哪些错,而不执行你写的程序。


## 运行

draw 画画的意思, 画画和设置 setup 是不一样的动作, 画画是一个连续的过程,设置只做一次。之所以画面一直在变,那是因为 draw() 在起作用，```draw(){}``` 里的指令，从第一行指令开始到最后一行一直在做**回圈 Loop** 运行。

说到这里我们就要引入一个概念 “帧” (Frame) 。

### 帧与程序帧
帧的概念是动画的概念,也是现代影像技术的概念。由于帧速率的变化构成了会动的影像,所以我们可以把影像理解成一种假象。由帧组成的动画,每一帧在表现运动的假象上有细微的差别，逐帧动画包含每一帧的图像或图像的描述，动态动画包含一幅图片的起始描述以及后续每一帧图像的变化规则。

Processing 的默认帧速率为 60fps , 打游戏的同学应该知道,尤其是格斗动作游戏, 顶尖高手最后会以每一招需要多少帧速率来计算,从而击败对方。

既然是默认的,同时也意味着我们可以修改它。

```java
     void setup() {
         frameRate(24);
         size(800,200);
         smooth();
         }

```

把它加入到setup()的程序块里头,告诉电脑,我要和电影播放一样的帧速率来运行动画。 所以我们可以把 draw() 里头的代码理解为程序帧,基于设置里头的帧速率运行。

和动画区别在于,动画是一组固定的图片序列, 逐一播放。而程序帧是动态的,图像是在程序运行时创建出来的

这也是程序的魅力所在之一，实时性 realtime ,提供了交互 interactive  的可能。

### 运算
因为变量 ```x, y``` 在 ```draw(){}```里不断地通过加法运算而改变,

```
     // Add the current speed to the location.
         x = x + xspeed;
         y = y + yspeed;
```

变量 x,y 随着程序的回圈运行，不断地在当前帧的基础上通过加法，让对象发生位移。


我们试错,拿掉 if 的程序快内容
```

         if ((x > width) || (x < 0)) {
             xspeed = xspeed * -1;
         }
         if ((y > height) || (y < 0)) {
             yspeed = yspeed * -1;
         }


```

现在又出现什么情况,程序并没有报错,只是球不见了,它跑出了画布,我们看不到它,但并不意味着它不存在了。



### 逻辑判断

if 作为如果, 是一个前提判断。前提为真,则结论为真,如果满足括号里的判断，则运行后面大括号里的内容。要是前提为假，则不运行大括号里的内容，直接跳过。

这就是程序的**逻辑**。

```
if ((x > width) || (x < 0)){
  xspeed = xspeed * -1;
  }
```

前提判断的括号中有两个判断 ```x > width``` 意味着当x大于画面的宽度 width ,```x < 0``` 意味着负数; 同理```y > height ``` 大于画面的高度。

processing 的画面作标原点位于画面的左上角,横轴x, 竖轴y, 是一个笛卡尔坐标系统 Cartesian coordinate system 的第一象限;
在这个坐标系统中每一个像素点为1个单位。


符号 || 代表着**或**的概念,只要两个条件里有一个为真,那么if的判断就为真。

我个人的理解是把这两竖想象成电路的**并联**,直要有一路时通的, 那 {} 这个灯泡里的程序就会亮. 当然还有类似**串联**的判断，之后细讲逻辑判断里会碰到。

真与假（ true，false ）做为布尔 boolean 这个数据类型。if()里只接受这个数据类型,来作出真假判断。

boolean 来自数学家布尔 Bool 的名字, 是布尔让逻辑具有了数学的运算可能,因此才会有了今天计算机的计算。

因为计算机只懂得开和关的信号,也就是0和1,也就是常听到的比特, 根据香浓的信息理论，bit 是最小的信息单位。我们可以把电脑的中央处理器 CPU (Center Processing Uinit)想象成由很多个继电器构成的东西,它在做着二进制的运算，处理这以bit 为单位的信息。


## 图像

最后画出圆的程序指令 ellipse(); 以及之前的几行代码
```
         // Display circle at x location
         stroke(0);
         strokeWeight(2);
         fill(127);
         ellipse(x, y, 48, 48);
```

ellipse 单词是椭圆的意思,为什么画面里是圆呢?

看后两位**参数**(parameter)都是48, 这其实是矩形的逻辑,所以我们其实并没有在画一个真正根具半径来计算的原型, 而是带有圆角的方形;前两位参数就是这个“圆”当前的位置。


前三行是关于这圆的属性,fill()是着色,stroke() 是对象边的颜色, strokeWeight()是边的宽度。 试着将小括号里的参数改改。

## 注解

应该已经注意到有一些开头 // 的语句，这些语句的内容主要是程序员给人看的，凡事是标有 // 的语句电脑在**编译**的时候一律越过。


## 总结

基本上关于这个球是如何运行的，我们根据代码的顺序已走了一遍：

首先初始位置的设定,在一个限定的画框里运动,随着程序帧里的变量变化,同时做了判断,让它不会跑出画面,只要程序不关,不断电,球永远在动。

在这个仅有32行的代码里，已经囊括了编程语言的最基本内容。

最后我们拿掉 ```draw(){}``` 里地一行的``` background();``` ,看看会发生什么? 


**基本概念**:帧、程序帧、数据类型、函数、布尔、浮点数、整数、变量、 指令、逻辑判断、逻辑运算、笛卡尔坐标、二进制
